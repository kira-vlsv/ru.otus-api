plugins {
    id "java-library"
    id "io.freefair.lombok" version "8.11"
    id "com.github.spotbugs" version "6.0.26"
    id "checkstyle"
    id "io.qameta.allure" version "2.12.0"
    id "maven-publish"
}

group = 'ru.otus'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.1'
    assertjVersion = '3.26.3'
    lombokVersion = '1.18.36'
    guiceVersion = '7.0.0'
    allureVersion = '2.29.0'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

allure {
    useJUnit5 {
        version = allureVersion
    }
}

dependencies {
    api "org.junit.jupiter:junit-jupiter:$junitVersion"
    api "io.rest-assured:rest-assured:5.5.0"
    api "com.fasterxml.jackson.core:jackson-core:2.15.2"
    api "com.fasterxml.jackson.core:jackson-databind:2.15.2"
    
    implementation "org.assertj:assertj-core:${assertjVersion}"
    implementation "org.projectlombok:lombok:${lombokVersion}"
    implementation "com.github.javafaker:javafaker:1.0.2"
    implementation "com.google.inject:guice:${guiceVersion}"
    implementation "io.qameta.allure:allure-rest-assured:${allureVersion}"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release.set(21)
}

checkstyle {
    toolVersion = '10.18.2'
    configFile = file("src/main/resources/checkstyle.xml")
}

spotbugs {
    toolVersion = '4.8.6'
    excludeFilter = file("${project.rootDir}/src/main/resources/excludeFilter.xml")
}

tasks.named("spotbugsMain").configure {
    reports {
        html.required.set(true)
        xml.required.set(false)
    }
}

tasks.named("spotbugsTest").configure {
    reports {
        html.required.set(true)
        xml.required.set(false)
    }
}

tasks.withType(Checkstyle) {
    reports {
        html.required.set(true)
        xml.required.set(false)
    }
}

tasks.named("test") {
    dependsOn("checkstyleMain", "checkstyleTest", "spotbugsMain", "spotbugsTest")
}

test {
    useJUnitPlatform() {
        if (project.hasProperty("tags")) {
            includeTags(project.property("tags") as String)
        }
    }
    systemProperty 'baseUrl', 'https://petstore.swagger.io/v2/pet/'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

task diagnoseEnvironment {
    doLast {
        println "========== Diagnose Environment =========="
        println "JAVA_HOME (env)         = ${System.getenv('JAVA_HOME')}"
        println "java.home (sys property)= ${System.getProperty('java.home')}"
        println "java.version            = ${System.getProperty('java.version')}"
        println "Java compiler version   = ${org.gradle.api.JavaVersion.current()}"
        println "=========================================="
    }
}

